# 普通类

```
ClassDeclaration:
	NormalClassDeclaration
	EnumDeclaration
NormalClassDeclaration:
	{ClassModifier} class Identifier [TypeParameters] [Superclass] [Superinterfaces] ClassBody
ClassModifier:
	(one of)
	Annotation public protected private abstract static final strictfp
TypeParameters:
	< TypeParameterList >
TypeParameterList:
	TypeParameter { , TypeParameter}
Superclass:
	extends ClassType
Superinterfaces:
	implements InterfaceTypeList
InterfaceTypeList:
	InterfaceType { , InterfaceType}
ClassBody:
	{ {ClassBodyDeclaration} }
ClassBodyDeclaration:
	ClassMemberDeclaration
	InstanceInitializer
	StaticInitializer
	ConstructorDeclaration
ClassMemberDeclaration:
	FieldDeclaration
	MethodDeclaration
	ClassDeclaration
	InterfaceDeclaration
;
```

其中，
- `Identifier`为类名，类不能与包围它的类或接口同名
- 可用的修饰符有：
    - `public`只能用于顶级类或成员类，不能用于局部类或匿名类
    - `protected`和`private`只能用于成员类
    - `abstract`用于使类不能用类实例创建表达式实例化（但非`abstract`子类可以），有`abstract`方法（声明或继承）的类必须为`abstract`。若不可能有子类实现抽象类的所有抽象方法，则抽象类声明不合法。
    - `static`只能用于成员类
    - `final`用于使类不能被扩展，不能与`abstract`一起用
    - `strictfp`用于使类声明中（包括嵌套类）所有浮点表达式成为浮点严格的
- 声明带类型参数的类叫泛型类
    - 类型变量不能依赖于自身（类型变量依赖于其界）
    - 泛型类不能是`Throwable`的直接或间接子类
    - 在以下场合不能引用泛型类的类型参数：
        - 其静态成员声明
        - 嵌套在其中的类声明的静态类型声明
        - 其静态初始化器
        - 嵌套在其中的类声明的静态初始化器
- 没有显式或隐式声明为`static`的嵌套类叫内部类，包括非静态成员类、局部类和匿名类。
    - 内部类不能声明静态初始化器
    - 内部类不能显式或隐式声明（但可以继承）非常变量的静态成员
    - 一个语句或表达式在静态上下文当且仅当最内层的方法、构造器、实例初始化器、静态初始化器、域初始化器或显式构造器调用为静态方法、静态初始化器、静态变量的初始化器或显式构造器调用
    - 内部类都有非静态上下文的直接外围类，内部类的对象则有直接外围对象
    - 内部类中使用但非声明的局部变量、形参和异常参数必须声明为`final`或实际上`final`
    - 内部类中使用但非声明的局部变量必须在内部类体前肯定赋值过
    - 词法上外围类型声明的空白`final`域不能在内部类中被赋值
- `extends`子句中指定一个可访问的非`final`类类型，不能是`Enum`或其调用
    - `Object`类声明中没有`extends`子句
    - 若`ClassType`有类型参数，必须为良构的参数化类型，类型参数不能为通配符
- 在超接口子句中指定直接超接口
    - 每个`InterfaceType`必须是可访问的接口类型
    - `InterfaceType`的类型参数（如有）必须是良构的参数化类型且没有参数为通配符
    - 相同的接口不能重复提及，即使用不同名提到
    - 一个类不能同时为同一泛型接口的不同参数化的子类型，也不能同时是同一泛型接口的参数化和原始类型的子类型
    - 除非类声明为`abstract`，否则必须实现（通过在类体声明或继承自超类/超接口）各直接超接口的所有`abstract`成员方法，一个方法声明可能实现多个超接口中的方法
- 一个类不能直接或间接依赖于自身（一个类依赖于它的父类和实现的接口及其限定类型），如果在运行期检测出则抛出`ClassCircularityError`

类成员包括：
- 继承自直接超类的可访问成员（除没有直接超类的`Object`类）
- 继承自直接超接口的可访问成员
- 类体中声明的成员

构造器、静态初始化器、实例初始化器不视为成员，更不会继承。

类的`public`和`protected`成员被其它包中声明的子类继承，`private`成员不会被子类继承。

方法类型由类型参数、参数类型、返回类型和抛出子句组成。

## 域声明

```
FieldDeclaration:
	{FieldModifier} UnannType VariableDeclaratorList ;
VariableDeclaratorList:
	VariableDeclarator { , VariableDeclarator}
VariableDeclarator:
	VariableDeclaratorId [ = VariableInitializer]
VariableDeclaratorId:
	Identifier [Dims]
VariableInitializer:
	Expression
	ArrayInitializer
UnannType:
	UnannPrimitiveType
	UnannReferenceType
UnannPrimitiveType:
	NumericType
	boolean
UnannReferenceType:
	UnannClassOrInterfaceType
	UnannTypeVariable
	UnannArrayType
UnannClassOrInterfaceType:
	UnannClassType
	UnannInterfaceType
UnannClassType:
	Identifier [TypeArguments]
	UnannClassOrInterfaceType . {Annotation} Identifier [TypeArguments]
UnannInterfaceType:
	UnannClassType
UnannTypeVariable:
	Identifier
UnannArrayType:
	UnannPrimitiveType Dims
	UnannClassOrInterfaceType Dims
	UnannTypeVariable Dims
FieldModifier:
	(one of)
	Annotation public protected private static final transient volatile
```
其中，一个域声明可声明多个域，
- 一个类中不能声明两个同名域
- 在类中声明域会隐藏所有超类或超接口中声明的可访问域（类型可以不同），被隐藏的域可以通过限定名、`super`或转换为超类访问。
- 类型`float`域的值来自`float`值集（不是扩展的），类型`double`域的值来自`double`值集（不是扩展的）
- 可用修饰符有：
    - `static`用于指出域关联于类，在类初始化时创建。没有声明为`static`的域称为实例变量，在创建一个对象时关联该类和它的超类声明的所有实例变量。
    - 空白`final`类变量必须由类的静态初始化器肯定赋值，空白`final`的实例变量必须在所有构造器结束时肯定赋值
    - `transient`用于指出变量不是对象持久化状态的一部分
    - `volatile`用于保证所有线程版变量的一致值，不能与`final`一起用

域声明的变量初始化器语义与赋值类似。

对于类变量，有以下：
- 初始化器不能通过简单名引用实例变量
- 初始化器不能用`this`或`super`
- 运行时在类初始化时初始化器求值和赋值刚好一次
- 常变量比其它`static`域更早初始化。

对于实例变量，有以下：
- 运行时在创建类实例时初始化器求值和赋值

使用后面声明的类变量不能让以下都成立：
- 类变量声明在`C`中的使用后
- 在类`C`的类变量初始化器或静态初始化器通过简单名使用
- 不是用在赋值的左方
- `C`为包围使用的最内层类或接口

使用后面声明的实例变量不能让以下都成立：
- 实例变量声明在`C`中的使用后
- 在类`C`的实例变量初始化器或实例初始化器通过简单名使用
- 不是用在赋值的左方
- `C`为包围使用的最内层类或接口

## 方法声明

```
MethodDeclaration:
	{MethodModifier} MethodHeader MethodBody
MethodHeader:
	Result MethodDeclarator [Throws]
	TypeParameters {Annotation} Result MethodDeclarator [Throws]
MethodDeclarator:
	Identifier ( [FormalParameterList] ) [Dims]
FormalParameterList:
	ReceiverParameter
	FormalParameters , LastFormalParameter
	LastFormalParameter
FormalParameters:
	FormalParameter { , FormalParameter}
	ReceiverParameter { , FormalParameter}
FormalParameter:
	{VariableModifier} UnannType VariableDeclaratorId
VariableModifier:
	(one of)
	Annotation final
ReceiverParameter:
	{Annotation} UnannType [Identifier . ] this
LastFormalParameter:
	{VariableModifier} UnannType {Annotation} ... VariableDeclaratorId
	FormalParameter
MethodModifier:
	(one of)
	Annotation public protected private abstract static final synchronized native strictfp
Result:
	UnannType
	void
Throws:
	throws ExceptionTypeList
ExceptionTypeList:
	ExceptionType { , ExceptionType}
ExceptionType:
	ClassType
	TypeVariable
MethodBody:
	Block
	;
```

其中，
- 一个类不能声明两个有覆盖等价签名的方法
    - 两个方法有相同签名的条件是名字相同且适应类型参数后有相同的形参类型
    - 一个方法的签名是另一个方法的子签名的条件是它们有相同签名或前者是后者的擦除
    - 两个方法有覆盖等价签名的条件是两者的签名互为对方的子签名
- 若`Result`是`void`，则方法不返回值，否则为返回类型，若返回类型为数组类型，部分方括号可放到形参列表后但不建议这样写。称返回类型`R`的声明`d`与返回类型`S`的声明`e`返回类型可替换，若
    - 若`R`为`void`则`S`为`void`
    - 若`R`为基本类型则`S`为相同的基本类型
    - 若`R`为引用类型则`S`则以下之一：
        - `R`适应`s`的类型参数后是`S`的子类型
        - `R`可通过未检查转换转换为`S`
        - `d`签名与`e`不同且`R = | S |`
- `ExceptionType`必须为`Throwable`的子类型，可以是类型变量
- 可用修饰符：
    - `abstract`方法引入成员方法但不提供实现，可覆盖非`abstract`方法。不能与`private`、`static`、`final`、`native`、`strictfp`或`synchronized`一起用。
    - `final`方法不能被覆盖或隐藏。`private`方法和`final`类中方法也不能覆盖。
    - `native`方法由平台相关代码实现，不能与`strictfp`一起用
    - `static`方法（类方法，与实例方法相对）的头和体不能用外围声明的类型参数，体不能用`this`、`super`
    - `strictfp`方法中所有`float`或`double`表达式为浮点严格的
    - `synchronized`在执行前获取监视器，类方法获取对应`Class`对象的，实例方法则获取`this`的
- 类型参数参照泛型类，称两个方法`M`（类型参数为`A1 , ..., An`）、`N`（类型参数为`B1 , ..., Bn`）类型参数相同当且仅当它们类型参数个数相同且对`1 ≤ i ≤ n`，`Ai`的界为对`Bi`的界应用`θ=[ B 1 := A 1 , ..., B n := A n ]`的结果。这时`N`提到的类型参数可以通过应用`θ`适应到`M`的类型参数。
- 当且仅当对`abstract`或`native`方法，体为`;`

一个类`C`从直接超类继承所有具体方法`m`（静态/实例）使以下都成立：
- `m`为`C`的直接超类成员
- `m`为`public`、`protected`或包访问且在同一包
- `C`中没有方法的签名是`m`签名的子签名

一个类`C`从直接超类或超接口`D`继承所有抽象和默认方法`m`使以下都成立：
- `m`为`D`的成员
- `m`为`public`、`protected`或包访问且在同一包
- `C`中没有方法的签名是`m`签名的子签名
- `C`没有继承签名为`m`签名的子签名的具体方法
- `C`的直接超类或直接超接口`D'`都不存在方法`m'`覆盖了`m`

类`C`声明或继承的实例方法`mC`从`C`覆盖类`A`中声明的方法`mA`使以下成立：
- `A`为`C`的超类
- `C`没有继承`mA`
- `mC`的签名是`mA`签名的子签名
- 以下之一成立：
    - `mA`为`public`
    - `mA`为`protected`
    - `mA`为包访问且以下之一：
        - `A`与`C`在同一包且，`mC`在`C`声明或是`C`直接超类的成员
        - `mC`从`C`的超类覆盖`mA`
        - `mC`从`C`覆盖方法`m'`（不是`mA`或`mC`），它从`C`的超类覆盖`mA`

若非`abstract`方法`mC`从`C`覆盖`abstract`方法`mA`，则称`mC`从`C`实现了`mA`。

类`C`声明或继承的实例方法`mC`覆盖接口`I`中声明的方法`mI`使以下成立：
- `I`为`C`的超接口
- `mI`为`abstract`或`default`方法
- `mC`的签名是`mI`签名的子签名

实例方法不能覆盖类方法。`strictfp`不影响重载。

被覆盖方法可以通过包含`super`的方法调用表达式访问，限定名和转换则没有用。

若类`C`声明或继承了静态方法`m`，其签名是`C`的超类或超接口中不然会可访问的方法`m'`的子签名，则前者隐藏了后者。静态方法不能隐藏实例方法。

被隐藏的方法可以通过限定名、带`super`的方法调用表达式和转换访问。

若返回类型为`R1`的方法声明覆盖或隐藏返回类型`R2`的方法声明，则`d1`必须与`d2`返回类型可替代。若`R1`不是`R2`的子类型，则编译期会发出未检查警告（如没被抑制）。

若类或接口`B`的方法`m2`覆盖超类或超接口`A`的方法`m1`，则：
- 若`m2`的`throws`子句提到检查型异常，则`m1`必须有抛出子句
- 对`m2`的`throws`子句提到的每个检查型异常，必须有超类型是`m1`抛出子句中异常的擦除
- 若`m1`抛出子句不包含`m2`抛出子句中每个异常类型的超类型（适应`m1`类型参数后），则出现编译期未检查异常

若类型声明`T`有成员方法`m1`，`T`或它的超类有方法`m2`，则以下不能同时成立：
- `m1`与`m2`同名
- `m2`在`T`可访问
- `m1`签名不是`m2`签名的子签名
- `m1`或它覆盖的方法签名与`m2`或它覆盖的方法签名有相同的擦除

覆盖或隐藏方法的访问修饰符至少提供被覆盖或隐藏方法提供的访问：
- 若被覆盖或隐藏方法为`public`，则覆盖或隐藏方法必须为`public`
- 若被覆盖或隐藏方法为`protected`，则覆盖或隐藏方法必须为`public`或`protected`
- 若被覆盖或隐藏方法为包访问，则覆盖或隐藏方法不能为`private`

一个类可以继承多个签名覆盖等价的方法。
- 一个类不能继承签名与其它继承方法覆盖等价的具体方法
- 一个类不能继承签名与其它继承方法覆盖等价的`default`方法，除非继承了超类中声明的与这些方法覆盖等价的抽象方法
- 若覆盖等价方法集包含至少一个abstract`方法，则类必须为`abstract`且继承所有这些方法
- 其中一个继承方法必须与所有其它继承方法返回类型可替代

若一个类有两个方法（声明或继承）同名但签名不是覆盖等价，则称方法名是重载的。

## 实例初始化器

```
InstanceInitializer:
	Block
```

实例初始化器在类实例创建时执行，它必须可正常完成，不能用`return`语句，可以用类型变量、`this`和`super`。

## 静态初始化器

```
StaticInitializer:
	static Block
```

静态初始化器在类初始化时执行，它必须可正常完成，不能用`return`语句，也不能用类型变量、`this`和`super`。

## 构造器

```
ConstructorDeclaration:
	{ConstructorModifier} ConstructorDeclarator [Throws] ConstructorBody
ConstructorDeclarator:
	[TypeParameters] SimpleTypeName ( [FormalParameterList] )
SimpleTypeName:
	Identifier
ConstructorModifier:
	(one of)
	Annotation public protected private
ConstructorBody:
	{ [ExplicitConstructorInvocation] [BlockStatements] }
ExplicitConstructorInvocation:
	[TypeArguments] this ( [ArgumentList] ) ;
	[TypeArguments] super ( [ArgumentList] ) ;
	ExpressionName . [TypeArguments] super ( [ArgumentList] ) ;
	Primary . [TypeArguments] super ( [ArgumentList] ) ;
```

其中，
- `SimpleTypeName`必须与类的简单名相同
- 形参的语义参照方法，只是非`private`内部类隐式地作为首个形参声明一个表示直接外围实例的变量
- 一个类的两个不同构造器的声明的擦除不能有覆盖等价的签名
- 一个类的两个不同构造器的声明的擦除不能相同
- 没有访问修饰符则默认为包访问
- `throws`子句的行为参照方法
- 构造器不能通过`this`直接或间接调用自己
- 若构造器体不由显式构造器调用而类不是`Object`，则构造器隐式由`super();`开始
- 除了可用显式构造器调用和不能显式返回值外,构造器的体参照方法的体。
- 显式构造器调用分为：
    - 替代构造器调用带`this`，用于调用相同类的另一构造器。执行过程为先从左到右求值参数再调用。
    - 超类构造器调用带`super`，用于调用直接超类的构造器。令`C`为待实例化的类，`S`为`C`的直接超类，`O`为`S`的直接外围类，执行过程为：
        1. 令`i`为正在创建的实例，要确定`i`关于`S`的直接外围实例：
            - 若`S`不是内部类或`S`的声明在静态上下文，则没有`i`关于`S`的直接外围实例
            - 对于非限定调用，则`S`为局部类或内部成员类，令`i`关于`S`的直接外围实例为相应外围实例
            - 对于限定的调用（`S`必须为内部类且其声明不在静态上下文），求值`Primary`或`ExpressionName`（其类型必须为`O`或其子类并且可访问），得到`null`则抛出`NullPointerException`使超类的构造器调用中止，否则`i`关于`S`的直接外围实例为其值
        2. 从左到右求值参数再调用构造器
        3. 超类构造器正常完成后按文本顺序求值所有实例变量初始化器和实例初始化器。

显式构造器调用中不能引用这类或其超类的实例变量、实例方法或内部类，也不能在表达式用`this`或`super`
显式构造器调用的类型参数不能为通配符

构造器可以重载，类似于方法调用，在类实例创建表达式解析。

若类没有构造器声明，则会隐式声明一个默认的：
- 可访问性与类相同
- 没有形参，除非是非`private`内部成员类，这时有一个形参表示直接外围实例
- 没有`throws`子句
- 体为对超类无参构造器的调用（`Object`类则为空），没有超类没有无参无`throws`子句的话是编译期错误

通过声明一个构造器且让所有构造器`private`可防止类外的代码创建类对象。类似可防止包外代码这样做。

# 枚举类

```
EnumDeclaration:
	{ClassModifier} enum Identifier [Superinterfaces] EnumBody
EnumBody:
	{ [EnumConstantList] [ , ] [EnumBodyDeclarations] }
EnumConstantList:
	EnumConstant { , EnumConstant}
EnumConstant:
	{EnumConstantModifier} Identifier [ ( [ArgumentList] ) ] [ClassBody]
EnumConstantModifier:
	Annotation
EnumBodyDeclarations:
	; {ClassBodyDeclaration}
```

其中，
- 不能用`abstract`或`final`
- 除非至少一个枚举常量有类体，否则枚举类型隐式地`final`
- 嵌套的枚举类型隐式地`static`，从而在内部类中不能声明枚举类型
- 枚举类型`E`的直接超类为`Enum<E>`
- 对于每个枚举常量
    - 枚举常量的可选参数在类初始化时会给构造器（按通常重载解析规则）
    - 可选的类体隐式声明了一个匿名类，它继承枚举类型，类体与其它匿名类类似，不能有构造器，其中实例方法可在枚举类型外调用仅当它们覆盖了枚举类型的可访问方法
    - 类体中不能声明`abstract`方法
    - 因为每个枚举常量只有一个实例，可用`==`比较两个对象是否对应同一枚举常量
- 构造器和成员声明与普通类类似，但有以下不同：
    - 构造器默认访问修饰符为`private`
    - 构造器不能为`public`或`protected`
    - 构造器不能有超类构造器调用语句
    - 构造器、实例初始化器和实例变量初始化表达式不能引用非常变量的`static`域
    - 没有声明构造器时默认构造器为`private`，没有形参也没有抛出子句
    - 若声明了`abstract`方法，则至少要有一个枚举常量且所有枚举常量都在类体提供其具体实现
    - 不能声明终结器，枚举常量不会终结

枚举类型仅有的实例由枚举常量定义，企图显式实例化枚举类型是编译期错误（反射、反序列化、clone也不行）。

枚举类型`E`成员有：
- `E`声明体中声明的成员
- 继承自`Enum<E>`的成员
- 对`E`的体中声明的每个枚举常量`c`，顺序隐式地在显式`static`域前声明类型`E`的`public static final`域与`c`同名，且与`c`有相同注解和初始化器。
称枚举常量在对应域初始化时创建。
- 下列隐式声明方法：

```java
/**
* Returns an array containing the constants of this enum
* type, in the order they're declared. This method may be
* used to iterate over the constants as follows:
*
*    for(E c : E.values())
*        System.out.println(c);
*
* @return an array containing the constants of this enum
* type, in the order they're declared
*/
public static E[] values();
/**
* Returns the enum constant of this type with the specified
* name.
* The string must match exactly an identifier used to declare
* an enum constant in this type. (Extraneous whitespace
* characters are not permitted.)
*
* @return the enum constant with the specified name
* @throws IllegalArgumentException if this enum type has no
* constant with the specified name
*/
public static E valueOf(String name);
```